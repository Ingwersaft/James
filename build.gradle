group = 'com.github.Ingwersaft'

buildscript {
    ext.kotlin_version = '1.2.31'
    ext.dokka_version = '0.9.15'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "net.researchgate:gradle-release:2.6.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.2"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "net.researchgate.release"
apply plugin: "com.jfrog.bintray"

apply plugin: 'application'
ext.main = 'com.mkring.james.JamesRuntimeKt'
mainClassName = main

apply plugin: 'maven-publish'

jar {
    manifest {
        attributes 'Main-Class': main
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.22.5'

    compile 'org.slf4j:slf4j-api:1.7.25'

    compile 'me.sargunvohra.lib:pokekotlin:2.3.0'                                   // poke api
    compile 'org.telegram:telegrambots:3.3'                                         // telegram api
    compile group: 'it.sauronsoftware.cron4j', name: 'cron4j', version: '2.2.5'     // jvm cron
    compile 'com.google.guava:guava:23.1-jre'                                       // cache

    compile "com.github.Ullink:simple-slack-api:1.2.0"

    compile 'com.neovisionaries:nv-websocket-client:2.3'
    compile 'com.google.code.gson:gson:2.8.0'

    testCompile "org.slf4j:slf4j-simple:1.7.25"                                         // logging
    testCompile 'junit:junit:4.12'

    testCompile "io.mockk:mockk:1.7.8"
    testCompile "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5"

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourceJar(type: Jar, dependsOn: classes) {
    from 'src/main/kotlin/'
    classifier = 'sources'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

build.finalizedBy(sourceJar, javadocJar)

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name rootProject.name
                    url "https://github.com/Ingwersaft/James"
                    licenses {
                        license {
                            name 'GNU General Public License v3.0'
                            url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

release {
    buildTasks = ['build']
    scmAdapters = [
            net.researchgate.release.GitAdapter
    ]
    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
        commitVersionFileOnly = false
        signTag = false
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenCustom']
    publish = true
    pkg {
        repo = 'JamesBot'
        name = project.group
        userOrg = user
        websiteUrl = 'https://github.com/Ingwersaft/James'
        issueTrackerUrl = 'https://github.com/Ingwersaft/James/issues'
        vcsUrl = 'https://github.com/Ingwersaft/James.git'

        labels = ['Chat', 'Chatbot', 'Kotlin', 'Slack', 'Telegram', 'Bot', 'RocketChat']

        version {
            name = project.version
        }
    }
}